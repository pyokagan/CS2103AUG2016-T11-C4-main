/*
 * Gradle Configuration File
 *
 * For more details take a look at the Java Quickstart chapter in the Gradle
 * user guide available at http://gradle.org/docs/2.2.1/userguide/tutorial_java_projects.html
 */

plugins {
    id 'java'
    id 'application'
    id 'jacoco'
    id 'checkstyle'
    id 'com.github.kt3k.coveralls' version '2.4.0'
    id 'com.github.johnrengelman.shadow' version '1.2.3'
}

repositories {
    jcenter()
    mavenCentral()
    maven { url "https://repo.eclipse.org/content/repositories/egit-releases/" }
    maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8
mainClassName = 'seedu.address.MainApp'
ext {
    if (!has('headless'))
        headless = 'false'
    if (!has('guiTests'))
        guiTests = 'true'
}

dependencies {
    compile 'org.controlsfx:controlsfx:8.40.11'
    compile 'com.fasterxml.jackson.core:jackson-databind:2.7.0'
    compile 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.7.4'
    compile 'com.google.guava:guava:19.0'

    testCompile 'junit:junit:4.12'
    testCompile 'org.testfx:testfx-core:4.0.+'
    testCompile 'org.testfx:testfx-junit:4.0.+'
    testCompile 'org.testfx:testfx-legacy:4.0.+', {
        exclude group: "junit", module: "junit"
    }
    testCompile 'org.testfx:openjfx-monocle:1.8.0_20'
}

shadowJar {
    archiveName = 'TaskTracker.jar'
    destinationDir = file("${buildDir}/jar/")
}

checkstyle {
    toolVersion = '7.1'
}

test {
    forkEvery = 100
    systemProperty 'testfx.setup.timeout', '60000'
    testLogging.exceptionFormat = 'full'

    include 'seedu/address/**'
    if (guiTests == 'true')
        include 'guitests/**'

    beforeTest { descriptor ->
        if (headless == 'true') {
            systemProperty 'java.awt.robot', 'true'
            systemProperty 'testfx.robot', 'glass'
            systemProperty 'testfx.headless', 'true'
            systemProperty 'prism.order', 'sw'
            systemProperty 'prism.text', 't2k'
        }
    }
}

jacocoTestReport {
    reports {
        html.enabled true
        xml.enabled true
        csv.enabled false
    }
}

tasks.coveralls.dependsOn jacocoTestReport

class PandocTask extends DefaultTask {

    @InputFiles
    List<File> inputFiles

    @InputDirectory
    File dataDir = project.file('pandoc')

    @Input
    File workDir

    @Input
    List<String> cmdArgs

    @InputFiles
    List<File> filters = []

    @OutputFile
    File outputFile

    @TaskAction
    def generate() {
        project.exec {
            workingDir workDir
            commandLine(['pandoc', '--self-contained', '-s',
                         "--data-dir=${dataDir}",
                         '-o', outputFile]
                        + filters.collect({ "--filter=${it}" })
                        + cmdArgs
                        + inputFiles.collect({it.toString()}))
        }
    }
}

task(pandoc, type: PandocTask) {
    workDir = project.file('docs')
    inputFiles = [project.file('docs/UserGuide.md'),
                               project.file('docs/DeveloperGuide.md')]
    cmdArgs = ['-f', 'markdown_github-hard_line_breaks+definition_lists+yaml_metadata_block+raw_tex',
               '-t', 'latex',
               '--latex-engine', 'xelatex']
    outputFile = new File(project.docsDir, 'manual.pdf')
    filters = [project.file('pandoc/githubonly.py')]
}

artifacts {
    archives shadowJar
}

defaultTasks 'build'
